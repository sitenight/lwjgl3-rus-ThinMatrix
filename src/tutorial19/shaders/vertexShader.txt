#version 400 core

in vec3 position; // позиции вершин из VAO
in vec2 textureCoords; // текстурные координаты из VAO
in vec3 normal; // вектор нормали из VAO

// отправляем в фрагментный шейдер
out vec2 pass_textureCoords; // текстурные координаты
out vec3 surfaceNormal; // нормаль поверхности
out vec3 toLightVector; // вектор направленный на источник света
out vec3 toCameraVector; // вектор положения камеры
out float visibility; // видимость вершины для тумана

uniform mat4 transformationMatrix; // матрица трансформации
uniform mat4 projectionMatrix; // матрица проекции
uniform mat4 viewMatrix; // матрица вида
uniform vec3 lightPosition; // позиция источника света
uniform float useFakeLighting; // использование фальшивого освещения

const float density = 0.0035; // плотность тумана
const float gradient = 5.0; // растояние перехода к нулевой видимости тумана

void main()
{
    vec4 worldPosition = transformationMatrix * vec4(position, 1.0);
    // положение вершины относительно камеры
    vec4 positionRelativeToCam = viewMatrix * worldPosition;

    // высчитываем новое положение вершины, в зависимости от трансформации и проекции
    gl_Position = projectionMatrix * positionRelativeToCam;

    vec3 actualNormal = normal; // актуальный вектор нормали
    // если использовать фальшивое освещение, то вектор нормали направим вверх
    if(useFakeLighting > 0.5) {
        actualNormal = vec3(0.0,1.0,0.0);
    }

    pass_textureCoords = textureCoords;
    // применяем трансформацию к нормали
    surfaceNormal = (transformationMatrix * vec4(actualNormal, 0.0)).xyz;
    toLightVector = lightPosition - worldPosition.xyz;

    // положение камеры относительно вершины
    toCameraVector = (inverse(viewMatrix) * vec4(0.0,0.0,0.0,1.0)).xyz - worldPosition.xyz;
    
    float distance = length(positionRelativeToCam.xyz); // расстояние до камеры
    visibility = exp(-pow((distance*density),gradient)); // расчет тумана
    visibility = clamp(visibility,0.0,1.0); // ограничиваем от 0 до 1
}
